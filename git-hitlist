#!/usr/bin/env perl
use strict;
use warnings;
use open qw/ :std :encoding(UTF-8) /;
use autodie;
use File::Temp 'tempfile';

open my $diff, "-|", qw(git diff -U0), @ARGV;

my (@hitlist, %file, %line);
while (<$diff>) {
    if (/^--- a\/(.+)/) {
        $file{"-"} = $1;
    } elsif (/^\+\+\+ b\/(.+)/) {
        $file{"+"} = $1;
    } elsif (/^@@ -(\d+)(?:,\d+)? \+(\d+)/) {
        %line = ("-" => $1, "+" => $2);
    } elsif (/^([-+])/) {
        next unless %line;
        chomp;
        push @hitlist, "$file{$1}:$line{$1}:$_";
        %line = ();
    }
}
close $diff;

unless (@hitlist) {
    warn "Nothing to whack.  (Perhaps you meant to use --cached?)\n";
    exit;
}

my ($tmpfh, $tmpfn) = tempfile();
print { $tmpfh } "$_\n" for @hitlist;
close $tmpfh;

system "vim", "-q", $tmpfn;
unlink $tmpfn;
